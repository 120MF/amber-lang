WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY) * }

program = { SOI ~ statement* ~ EOI }

statement = {
    declaration |
    assignment |
    expr_stmt |
    struct_def |
    function_def |
    impl_block
}

declaration = {
    modifier? ~              // comptime/runtime
    keyword ~                // let/var
    ident ~                  // variable name
    (":" ~ type_def)? ~      // optional type def
    ("=" ~ expr)? ~          // optional init value
    ";"
}

modifier = { "comptime" | "runtime" }
keyword = { "let" | "var" }

assignment = { ident ~ "=" ~ expr ~ ";" }
expr_stmt = { expr ~ ";" }
return_stmt = { "return" ~ expr? ~ ";" }

type_def = { builtin_type | ident }
builtin_type = { "u8" | "u16" | "u32" | "u64" | "i8" | "i16" | "i32" | "i64" | "f32" | "f64" | "bool" }

atom = { float_lit | int_lit | bool_lit | ident | "(" ~ expr ~ ")" }

bin_op = _{ add | sub | mul | div }
  add = {"+"}
  sub = {"-"}
  mul = {"*"}
  div = {"/"}

expr = { atom ~ (bin_op ~ atom)* }

ident =   @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
int_lit = @{ ASCII_DIGIT+ }
float_lit = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ ( "f" | "d" )? }
bool_lit = @{ "true" | "false" }

struct_def = { "struct" ~ ident ~ "{" ~ struct_fields? ~ "}" }
struct_fields = { struct_field ~ ("," ~ struct_field)* ~ ","? }
struct_field = { ident ~ ":" ~ type_def }

function_def = { extern_modifier? ~ "fn" ~ ident ~ parameter_list ~ return_type? ~ function_body }
extern_modifier = { "extern" }
parameter_list = { "(" ~ (param ~ ("," ~ param)*)? ~ ")" }
param = { param_self | param_typed }
param_self = { "self" }
param_typed = { ident ~ ":" ~ type_def }
return_type = { "->" ~ type_def }
function_body = { block | ";" }

block = { "{" ~ (declaration | assignment | expr_stmt | return_stmt)* ~ "}" }

impl_block = { "impl" ~ ident ~ "{" ~ function_def* ~ "}" }
